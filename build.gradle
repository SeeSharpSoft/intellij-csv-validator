plugins {
    // https://github.com/JetBrains/gradle-intellij-plugin
    id 'org.jetbrains.intellij' version '1.9.0'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
    id 'com.github.ManifestClasspath' version '0.1.0-RELEASE'
    // https://github.com/JetBrains/gradle-grammar-kit-plugin
    id 'org.jetbrains.grammarkit' version '2021.2.2'
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

group 'net.seesharpsoft.intellij.plugins'
version '3.0.0'

apply plugin: 'java'
project.sourceCompatibility = JavaVersion.VERSION_11
project.targetCompatibility = JavaVersion.VERSION_11
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:deprecation"
}
repositories {
    mavenCentral()
}
dependencies {
    implementation 'net.seesharpsoft.sharping:sharping-commons:0.21.0'
    compileOnly 'org.apache.ant:ant:1.10.12'
    testImplementation 'org.mockito:mockito-core:4.8.1'
}
sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'gen']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

apply plugin: 'idea'
idea {
    project {
        jdkName = JavaVersion.VERSION_11
        languageLevel = JavaVersion.VERSION_11
        vcs = 'Git'
    }
    module {
        generatedSourceDirs += file('gen')
    }
}

apply plugin: 'org.jetbrains.intellij'
intellij {
    // IDE version - https://www.jetbrains.com/intellij-repository/releases
    version = System.getenv().getOrDefault('IDEA_VERSION', 'IC-2022.1.1')
    pluginName = 'CSV'
    updateSinceUntilBuild = false
    downloadSources = Boolean.parseBoolean(System.getenv().getOrDefault('IDEA_SOURCES', "true"))

    plugins = [
            System.getenv().getOrDefault('GIT_PLUGIN_VERSION', 'Git4Idea'),
            System.getenv().getOrDefault('GITHUB_PLUGIN_VERSION', 'org.jetbrains.plugins.github')
    ]
}
patchPluginXml {
    changeNotes = """<pre style="font-family: sans-serif">
NEW: major update release!
      </pre>"""
}
publishPlugin {
    token = System.getenv().getOrDefault('JI_TOKEN', '')
    channels = [System.getenv().getOrDefault('JI_CHANNELS', 'Testing')]
}

test {
    jacoco {
        enabled = true
    }
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
    // see https://youtrack.jetbrains.com/issue/IDEA-278926
    scanForTestClasses = false
    include "**/*Test.class"
}

grammarKit {
    // version of IntelliJ patched JFlex - https://bintray.com/jetbrains/intellij-third-party-dependencies/jflex
    jflexRelease = '1.7.0-1'
    // tag or short commit hash of Grammar-Kit to use - https://github.com/JetBrains/Grammar-Kit
    grammarKitRelease = System.getenv().getOrDefault('GRAMMAR_KIT_VERSION', '2021.1.2')
}

tasks.named("generateParser").configure {
    source = 'src/main/java/net/seesharpsoft/intellij/plugins/csv/Csv.bnf'
    targetRoot = 'gen'
    pathToParser = '/net/seesharpsoft/intellij/plugins/csv/parser/CsvParser.java'
    pathToPsiRoot = '/net/seesharpsoft/intellij/plugins/csv/psi'
    purgeOldFiles = true
}

tasks.named("generateLexer").configure {
    dependsOn generateParser

    source = 'src/main/java/net/seesharpsoft/intellij/plugins/csv/CsvLexer.flex'
    targetDir = './gen/net/seesharpsoft/intellij/plugins/csv/'
    targetClass = 'CsvLexer'
    purgeOldFiles = true
}

compileJava {
    dependsOn generateLexer
}

// TODO https://youtrack.jetbrains.com/issue/IDEA-298989 - remove after gradle plugin v1.9.1 or v1.10.0 released
setupDependencies {
    doLast {
        fileTree("$buildDir/instrumented/instrumentCode") { include("**/*TableEditorSwing.class") }.files.forEach { delete(it) }
    }
}