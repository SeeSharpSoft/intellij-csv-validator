{
  parserClass="net.seesharpsoft.intellij.plugins.csv.parser.CsvParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Csv"
  psiImplClassSuffix="Impl"
  psiPackage="net.seesharpsoft.intellij.plugins.csv.psi"
  psiImplPackage="net.seesharpsoft.intellij.plugins.csv.psi.impl"

  elementTypeHolderClass="net.seesharpsoft.intellij.plugins.csv.psi.CsvTypes"
  elementTypeClass="net.seesharpsoft.intellij.plugins.csv.psi.CsvElementType"
  tokenTypeClass="net.seesharpsoft.intellij.plugins.csv.psi.CsvTokenType"

  tokens=[
    TEXT
    ESCAPED_TEXT
    COMMA
    QUOTE
    CRLF
    COMMENT
   ]
}

csvFile ::= record (CRLF record)* [CRLF]

record ::= COMMENT | (field (COMMA field)*)

field ::= escaped | nonEscaped

private escaped ::= QUOTE quotedText QUOTE

private quotedText ::= (TEXT | ESCAPED_TEXT)*

private nonEscaped ::= TEXT*
